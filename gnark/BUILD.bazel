# Copyright 2025 The RabbitSNARK Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ==============================================================================

load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_cc//cc:defs.bzl", "cc_import")
load("@zkx//bazel:zkx_cc.bzl", "zkx_cc_library", "zkx_cc_test", "zkx_openmp_defines")
load(":build_defs.bzl", "if_has_sp1")

package(default_visibility = ["//visibility:public"])

bool_flag(
    name = "has_sp1",
    build_setting_default = False,
)

config_setting(
    name = "zkx_has_sp1",
    flag_values = {":has_sp1": "true"},
)

cc_import(
    name = "babybear_cc",
    hdrs = ["babybear.h"],
    static_library = if_has_sp1("libbabybear.a", None),
)

zkx_cc_library(
    name = "command_runner_impl",
    hdrs = ["command_runner_impl.h"],
    defines = if_has_sp1(
        ["ZKX_HAS_SP1"],
        [],
    ) + zkx_openmp_defines(),
    deps = [
        "//common:command_runner_interface",
        "//common:profiler",
        "//gnark/bin:proof_writer",
        "//gnark/bin:public_writer",
        "//gnark/hlo:hlo_generator",
        "//gnark/pk:proving_key",
        "//gnark/witness",
        "@com_google_absl//absl/strings",
        "@zkx//xla/tsl/platform:env",
        "@zkx//xla/tsl/platform:errors",
        "@zkx//xla/tsl/platform:path",
        "@zkx//xla/tsl/platform:statusor",
    ] + if_has_sp1(
        [
            ":babybear_cc",
            ":sp1_cc",
        ],
        [
            "@gnark_go//:gnark_cc",
        ],
    ),
)

zkx_cc_library(
    name = "reader_utils",
    srcs = ["reader_utils.cc"],
    hdrs = ["reader_utils.h"],
    deps = [
        "@com_google_absl//absl/status",
        "@zkx//xla/tsl/platform:errors",
        "@zkx//zkx/base/buffer:endian_auto_reset",
        "@zkx//zkx/base/buffer:read_only_buffer",
    ],
)

zkx_cc_library(
    name = "serde_mode",
    srcs = ["serde_mode.cc"],
    hdrs = ["serde_mode.h"],
    deps = [
        "@com_google_absl//absl/base:core_headers",
        "@zkx//zkx/math/elliptic_curves/short_weierstrass:affine_point",
    ],
)

cc_import(
    name = "sp1_cc",
    hdrs = ["libsp1.h"],
    static_library = if_has_sp1("libsp1.a", None),
)

zkx_cc_test(
    name = "gnark_test",
    srcs = ["command_runner_test.cc"],
    data = [
        "r1cs.bin",
        "//gnark/pk:pk.dump.bin",
        "//gnark/witness:witness.bin",
    ],
    # TODO(chokobole): This takes too long.
    tags = ["manual"],
    deps = [
        ":command_runner_impl",
        "//gnark/bin:proof_reader",
        "//gnark/bin:public_reader",
        "@zkx//xla/tsl/platform:statusor",
        "@zkx//zkx:literal",
        "@zkx//zkx:literal_util",
        "@zkx//zkx/math/elliptic_curves/bn/bn254:curve",
    ],
)
